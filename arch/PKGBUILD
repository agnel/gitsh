# Maintainer: Pat Brisbin <pbrisbin@gmail.com>
pkgname='gitsh'
pkgver=0.14
pkgrel=1
pkgdesc='An interactive shell for git'
arch=('any')
url="http://thoughtbot.github.io/gitsh/"
license=('custom')
depends=('ruby>=2.3.0' 'readline')
source=("http://thoughtbot.github.io/gitsh/gitsh-0.14.tar.gz")
sha256sums=('4b89b6d006326a7b57c4c8e440594e477db61b7d3fe2633a8aad176bb19d0125')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  ./configure \
     --disable-debug \
     --disable-dependency-tracking \
     --disable-silent-rules \
     --prefix=/usr

  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:
